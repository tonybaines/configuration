apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'codenarc'
apply plugin: 'jdepend'
apply plugin: 'build-dashboard'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'signing'
apply plugin: 'java-library-distribution'
buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-nexus-plugin:2.2'
  }
}
apply plugin:  'com.bmuschko.nexus'

group = 'com.github.tonybaines'
version = '0.8.7-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
}
dependencies {
  compile([
    'org.codehaus.groovy:groovy-all:2.4.1',
    'com.google.guava:guava:15.0',
    'org.slf4j:slf4j-api:1.6.1',
    'org.hibernate:hibernate-validator:5.1.0.CR1',
    'javax.el:javax.el-api:2.2.4',
    'org.glassfish.web:javax.el:2.2.4',
  ])

  testCompile([
    'org.spockframework:spock-core:1.0-groovy-2.4',
  ])

  testRuntime([
    'org.objenesis:objenesis:1.2',
    'org.slf4j:slf4j-simple:1.6.1',
  ])
  runtime([])
}

/* The java-library-distribution plugin*/
distributions {
  main {
    baseName = "${project.group}-${project.name}"
  }
}

/* The Nexus plugin */
modifyPom {
  project {
    name 'Gestalt'
    packaging 'jar'
    description 'Gestalt is a library for making configuration easier in Java'
    url 'https://github.com/tonybaines/gestalt'

    scm {
      url 'git@github.com:tonybaines/gestalt.git'
      connection 'scm:git@github.com:tonybaines/gestalt.git'
      developerConnection 'scm:git@github.com:tonybaines/gestalt.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'tonybaines'
        name 'Tony Baines'
      }
    }
  }
}

nexus {
  sign = true
}

extraArchive {
  sources = true
  tests = true
  javadoc = true
}

/* Static Analysis */
codenarc {
  ignoreFailures = true
  configFile = file("${project.projectDir}/config/codenarc/codenarc.groovy")
  toolVersion = "0.20"
}
tasks.withType(CodeNarc) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}
tasks.withType(JDepend) {
  reports {
    xml.enabled = false
    text.enabled = true
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.2'
}