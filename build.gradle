apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'build-dashboard'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'signing'
apply plugin: 'java-library-distribution'
buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
  }
}
apply plugin: 'nexus'

group = 'alltehcode'
version = '0.3.0'
dependencies {
  compile([
    'org.codehaus.groovy:groovy-all:2.2.1',
    'com.google.guava:guava:14.0.1',
    'org.slf4j:slf4j-simple:1.6.1',
    'org.hibernate:hibernate-validator:5.1.0.CR1',
    'javax.el:javax.el-api:2.2.4',
    'org.glassfish.web:javax.el:2.2.4',
  ])

  testCompile([
    'org.spockframework:spock-core:0.7-groovy-2.0',
  ])

  testRuntime([
    'org.objenesis:objenesis:1.2',
  ])
  runtime([])
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

/* The java-library-distribution plugin*/
distributions {
  main {
    baseName = "${project.group}-${project.name}"
  }
}

/* The Nexus plugin */
modifyPom {
  project {
    name 'Gestalt'
    packaging 'jar'
    description 'Gestalt is a library for making configuration easier in Java'
    url 'https://github.com/tonybaines/gestalt'

    scm {
      url 'scm:git@github.com:tonybaines/gestalt.git'
      connection 'scm:git@github.com:tonybaines/gestalt.git'
      developerConnection 'scm:git@github.com:tonybaines/gestalt.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'tonybaines'
        name 'Tony Baines'
      }
    }
  }
}

nexus {
  attachSources = true
  attachTests = true
  attachJavadoc = false
  sign = true
}

/* Static Analysis */
tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}
tasks.withType(JDepend) {
  reports {
    xml.enabled = false
    text.enabled = true
  }
}
