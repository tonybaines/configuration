buildscript {
  ext.kotlin_version = '1.2.51'
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}
plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id 'org.owasp.dependencycheck' version '3.2.1'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.3'
  id 'com.dorongold.task-tree' version '1.3'
}

apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'codenarc'
apply plugin: 'jdepend'
apply plugin: 'build-dashboard'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'signing'
apply plugin: 'java-library-distribution'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.github.tonybaines'
version = '2.0.14'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}
dependencies {
  compile([
    "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
    /* Switching to Groovy 2.5 caused  test failures around casting */
    'org.codehaus.groovy:groovy-all:2.4.15',
    'com.google.guava:guava:25.1-jre',
    'org.slf4j:slf4j-api:1.7.25',
    'commons-beanutils:commons-beanutils:1.9.3',
    'org.apache.geronimo.specs:geronimo-validation_1.0_spec:1.1',
  ])
  compile('org.apache.bval:org.apache.bval.bundle:0.5'){
    // CVE-2014-0114
    exclude group: 'commons-beanutils', module: 'commons-beanutils-core'
  }

  testCompile([
    'org.spockframework:spock-core:1.1-groovy-2.4',
  ])

  testRuntime([
    'org.objenesis:objenesis:2.6',
    'org.slf4j:slf4j-simple:1.7.25',
  ])
  runtime([])
}

dependencyCheck {
  suppressionFile = 'config/dependency-check-suppressions.xml'
  format = 'ALL'
  failBuildOnCVSS = 3
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'dev'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

/* The java-library-distribution plugin*/
distributions {
  main {
    baseName = "${project.group}-${project.name}"
  }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
task testSourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'tests'
  from sourceSets.test.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
// add javadoc/source/test jar tasks as artifacts
artifacts {
  archives sourcesJar, javadocJar, testSourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact testSourcesJar
      artifact javadocJar

      pom.withXml {
        asNode().appendNode('name', 'gestalt')
        asNode().appendNode('description', 'Gestalt is a library for making configuration easier in Java')
        asNode().appendNode('url', 'https://github.com/tonybaines/gestalt')

        def scm = asNode().appendNode('scm')
        scm.appendNode('url', 'git@github.com:tonybaines/gestalt.git')
        scm.appendNode('connection', 'scm:git@github.com:tonybaines/gestalt.git')
        scm.appendNode('developerConnection', 'scm:git@github.com:tonybaines/gestalt.git')

        def lic = asNode().appendNode('licenses')
          .appendNode('license')
        lic.appendNode('name', 'The Apache Software License, Version 2.0')
        lic.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        lic.appendNode('distribution', 'repo')

        def dev = asNode().appendNode('developers')
          .appendNode('developer')
        dev.appendNode('id', 'tonybaines')
        dev.appendNode('name', 'Tony Baines')

        // Workaround for the maven-publish-plugin marking all deps as runtime scope
        // http://forums.gradle.org/gradle/topics/maven_publish_plugin_generated_pom_making_dependency_scope_runtime
        asNode().dependencies.'*'.findAll() {
          it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
            dep.name == it.artifactId.text()
          }
        }.each() {
          it.scope*.value = 'compile'
        }
      }
    }
  }
}

String safeGetProp(String name) { hasProperty(name) ? getProperty(name) : System.getenv(name) }

bintray {
  user = safeGetProp('bintrayUser')
  key = safeGetProp('bintrayApiKey')

  publications = ['mavenJava'] //When uploading Maven-based publication files

  dryRun = false //Whether to run this as dry-run, without deploying
  publish = true //If version should be auto published after an upload
  pkg {
    repo = 'maven'
    name = "${project.group}:${project.name}"
    desc = 'A JVM config framework written in Groovy'
    websiteUrl = 'https://github.com/tonybaines/gestalt'
    issueTrackerUrl = 'https://github.com/tonybaines/gestalt/issues'
    vcsUrl = 'https://github.com/tonybaines/gestalt.git'
    licenses = ['Apache-2.0']
    labels = ['configuration', 'groovy', 'java']
    publicDownloadNumbers = true
    //Optional version descriptor
    version {
      gpg {
        sign = true //Determines whether to GPG sign the files. The default is false
        passphrase = safeGetProp('signing.password')
      }
      mavenCentralSync {
        sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
        user = safeGetProp('nexusUsername') //OSS user token
        password = safeGetProp('nexusPassword') //OSS user password
        close = '1'
        //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by putting 0 as value) and release the version manually.
      }
    }
  }
}

/* Static Analysis */
codenarc {
  ignoreFailures = true
  configFile = file("${project.projectDir}/config/codenarc/codenarc.groovy")
  toolVersion = "0.27.0"
}
tasks.withType(CodeNarc) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}
tasks.withType(JDepend) {
  reports {
    xml.enabled = false
    text.enabled = true
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions.jvmTarget = "1.8"
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.9'
  distributionType = Wrapper.DistributionType.ALL
}
