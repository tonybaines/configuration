apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'maven-publish'

group = 'tonybaines.configuration'
version = '0.1-SNAPSHOT'

dependencies {
  compile([
    'org.codehaus.groovy:groovy-all:2.2.1',
    'com.google.guava:guava:14.0.1',
    'org.slf4j:slf4j-simple:1.6.1',
    'org.hibernate:hibernate-validator:5.1.0.CR1',
    'javax.el:javax.el-api:2.2.4',
    'org.glassfish.web:javax.el:2.2.4',
  ])

  testCompile([
    'org.spockframework:spock-core:0.7-groovy-2.0',
  ])

  testRuntime([
    'org.objenesis:objenesis:1.2',
  ])
  runtime([])
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}


task release(type: Zip, dependsOn: ['clean', 'jar', 'generatePomFileForMavenJavaPublication']) {
  from('build/publications/mavenJava/pom-default.xml') {
    rename 'pom-default.xml', 'pom.xml'
  }
  from("build/libs/${project.name}-${project.version}.jar") {
    rename "${project.name}-${project.version}.jar", "${project.group}.${project.name}-${project.version}.jar"
  }
  baseName "${project.group}.${project.name}"
}


tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}
tasks.withType(JDepend) {
  reports {
    xml.enabled = false
    text.enabled = true
  }
}
